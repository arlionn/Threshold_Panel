
#***************************************************************************
# EXAMPLE: Replication of Hansen's results in Journal of Econometrics (1999)
#***************************************************************************
# NOTE: For some reasons Hansen doesn't exploit the whole dataset comprising
# 14 time observations (one lost due to lags) but actually only 13 if one
# looks at his code. Hence, our results based on the full unrestricted
# dataset do not fully coincide!
set verbose off
clear
include "/home/at/git/Threshold_Panel/Threshold_Panel.inp"
#open ThresholdPanel_data_joe99.gdt --frompkg=Threshold_Panel
open "/home/at/git/Threshold_Panel/ThresholdPanel_data_joe99.gdt" --quiet

# *** Gretl versions starting with 2016d should handle this
# *** automatically, but in case of problems please install
# *** the "wooldridge_test_serial" package by hand.
if $version >= 20164
    catch include wooldridge_test_serial.gfn
    if $error
        install wooldridge_test_serial
        include wooldridge_test_serial.gfn
    endif
else
  # install wooldridge_test_serial
  include wooldridge_test_serial.gfn
endif

#-----------------
# Dataset settings
#-----------------
setobs 15 1:1 --stacked-time-series
rename v1 inva
rename v2 vala
rename v3 cfa
rename v4 debta

#------------------------
# SET UP MODEL PARAMETERS
#------------------------
scalar nthresh = 2						# Max. # of thresholds to test, Hansen (1999) ntresh=3
scalar qn = 400							# Hansen (1999) qn=400
scalar nrep = 300						# Hansen (1999) nrep=300
matrix trim = {0.01, 0.01}				# Hansen (1999) trim_1=0.01, trim_2=0.01, trim_3=0.05
scalar alpha = 0.2						# 0<alpha<1: Choose a rather high value if you want definitely to estimate the refinement coefficient values
scalar conf_lev = 0.95					# Hansen (1999) conf_lev=0.95 for LR-test on H0: gamma=gamma0
scalar TF = 0							# Time-effects: 0=NO (Hansen, 1999), 1=YES

#--------------------------------
# Shrink dataset for illustration
# Note: Using the complete sample
# takes a very long time 
#--------------------------------
#smpl 1:01 20:15

#------------------------------
# Set up the series of interest
#------------------------------
series vala2 = vala^2
series vala3 = vala^3
series debtval = debta*vala
list exo = vala vala2 vala3 debta debtval
list exo1 = exo(-1)
list rexo = cfa
list rexo1 = lags(1,rexo)
series thresh = debta(-1)
list Lall = inva exo1 rexo1 thresh
# Make sure you have a balanced dataset
smpl Lall --balanced --no-missing

#-------------------
# Run the procedure
#-------------------
set stopwatch
bundle B = THRESH_SETUP(inva,exo1,rexo1,thresh, \
  nthresh, TF, qn, nrep, trim, alpha, conf_lev)
printf "\n This took %.2f seconds\n", $stopwatch

#---------------
# Bundle output
#---------------
print B

#---------------------------------------------------
# Plot the Likelihood Ratio test confidence interval
# jointly with the 'no-rejection region' using the
# likelihood ratio statistic for tests on gamma.
#---------------------------------------------------
matrix qq = B.qq			# thresholds tested
matrix lrstats = B.lrstats	# LR test statistics
string fname = "display" 	# Path+filename or "display"
lrplot(lrstats, qq, fname)




