set verbose off
clear

/* Replication of Hansen's results in Journal of Econometrics (1999)

 NOTE: For some reasons Hansen doesn't exploit the whole dataset comprising
 14 time observations (one lost due to lags) but actually only 13 if one
 looks at his code. Hence, our results based on the full unrestricted
 dataset do not fully coincide!
*/

include Threshold_Panel.gfn
open ThresholdPanel_data_joe99.gdt --frompkg=Threshold_Panel


# Define a panel data set + provide meaningful series names
setobs 15 1:1 --stacked-time-series
rename v1 inva
rename v2 vala
rename v3 cfa
rename v4 debta

# Shrink dataset for illustration purpose only
# Note: Using the complete sample takes a very long time
smpl 1:01 25:15

# Set up the series of interest
series vala2 = vala^2
series vala3 = vala^3
series debtval = debta*vala
list exo = vala vala2 vala3 debta debtval
list exo1 = exo(-1)
list rexo = cfa
list rexo1 = lags(1,rexo)
series thresh = debta(-1)
list Lall = inva exo1 rexo1 thresh
# Make sure you have a balanced dataset
smpl Lall --balanced --no-missing


# Run the procedure with default values
set stopwatch
bundle Model = estimate_tpm(inva, exo1, rexo1, thresh)
printf "\n This took %.2f seconds\n", $stopwatch

print Model         # print bundle output

/* Plot the Likelihood Ratio test confidence interval jointly with the
'no-rejection region' using the LR-statistic for tests on gamma. */
plot_tpm(Model)


/* Activate optionally
# Run the procedure with you own values
bundle opts = _(nthresholds = 2,\
                nboot = 199,\
                confidence_level = 0.8)
bundle Model2 = estimate_tpm(inva, exo1, rexo1, thresh, opts)

plot_tpm(Model2)
*/
